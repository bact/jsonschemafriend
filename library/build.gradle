plugins {
    id 'java-library'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'jimblackler.jsonschemafriend.core')
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                group = 'net.jimblackler.jsonschemafriend'
                artifactId = 'core'
                version = '0.12.3'
                artifact(sourceJar)
                artifact(javadocJar)
                from components.java
                pom {
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

test {
    useJUnitPlatform()

    systemProperty "run.smoke.test", project.findProperty('run.smoke.test') ?: false

    testLogging {
        showStandardStreams = false
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
    }
}

dependencies {
    implementation 'com.damnhandy:handy-uri-templates:2.1.8'
    implementation 'com.ibm.icu:icu4j:74.2'
    implementation 'commons-validator:commons-validator:1.8.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'org.jruby.joni:joni:2.2.1'
    implementation 'org.jsoup:jsoup:1.17.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testImplementation 'org.json:json:20231013'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.16.1'
}
