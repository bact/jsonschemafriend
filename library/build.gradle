plugins {
    id 'java-library'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                group = 'net.jimblackler.jsonschemafriend'
                artifactId = 'core'
                version = '0.10.6'
                artifact(sourceJar)
                artifact(javadocJar)
                from components.java
                pom {
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'com.damnhandy:handy-uri-templates:2.1.8'
    implementation 'com.ibm.icu:icu4j:69.1'
    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.5'
    implementation 'net.jimblackler:usejson:0.2.1'
    implementation 'org.jsoup:jsoup:1.14.2'
    testImplementation project(path: ':extra')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.json:json:20210307'
}
